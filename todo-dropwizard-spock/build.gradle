apply plugin: 'groovy'
apply plugin: 'gradle-one-jar'
apply plugin: 'application'
apply plugin: 'idea'

// Use Java 7 by default
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.gettingagile.todo.TodoService'

// Add Gradle OneJar Plugin, see https://github.com/rholder/gradle-one-jar
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.6.2'
}

repositories {
    mavenCentral()
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:2.1.9'
    compile (
            'com.yammer.dropwizard:dropwizard-core:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-migrations:' + dropwizardVersion,
            'com.yammer.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'com.sun.jersey:jersey-client:1.12',
            'com.h2database:h2:1.3.168'
    )
    testCompile (
            'org.spockframework:spock-core:0.7-groovy-2.0',
            'cglib:cglib-nodep:2.2.2'
    )
}

// Configure the oneJar task
task oneJar(type: OneJar) {
    mainClass = mainClassName
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './src/dist/config/todo.yml'

}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

artifacts {
    oneJar
}
